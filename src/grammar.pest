WHITESPACE = _{ " " }

equation = _{ SOI ~ expr ~ EOI }
  expr = { primary ~ (infix ~  primary)* }
    infix = _{ add | sub | mul | div | rem | pow }
      add = { "+" }
      sub = { "-" }
      mul = { "*" }
      div = { "/" }
      rem = { "%" }
      pow = { "^" }
    primary = _{ number ~ postfix? | prefix? ~ atom ~ postfix? }
      number = @{
        "-"?
        ~ ("0" | ASCII_NONZERO_DIGIT ~ ASCII_DIGIT*)
        ~ ("." ~ ASCII_DIGIT*)?
        ~ (^"e" ~ ("+" | "-")? ~ ASCII_DIGIT+)?
      }
      prefix = _{ neg }
        neg = { "-" }
      postfix = _{ fac }
        fac = { "!" }
    atom = _{ binaryfncall | unaryfncall | "(" ~ expr ~ ")"}
      unaryfncall = { unaryfn ~ "(" ~ expr ~ ")" }
        unaryfn = { "sin" }
      binaryfncall = { binaryfn ~ "(" ~ expr ~ "," ~ expr ~ ")" }
        binaryfn = { "log" }
